buildscript {
	repositories { jcenter() }

	dependencies {
		classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
	}
}

//apply plugin: 'nebula-aggregate-javadocs'
import org.gradle.api.*

Project rootProject = project.rootProject
rootProject.gradle.projectsEvaluated {
	Set<Project> javaSubprojects = rootProject.subprojects.findAll { subproject -> subproject.plugins.hasPlugin(JavaPlugin) }
	if (!javaSubprojects.isEmpty()) {
		rootProject.task("aggregateJavadocs", type: Javadoc) {
			title = '<h1>Reveno Framework </h1><script>\n' +
					'  (function(i,s,o,g,r,a,m){i[\'GoogleAnalyticsObject\']=r;i[r]=i[r]||function(){\n' +
					'  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),\n' +
					'  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)\n' +
					'  })(window,document,\'script\',\'//www.google-analytics.com/analytics.js\',\'ga\');\n' +
					'\n' +
					'  ga(\'create\', \'UA-19993890-2\', \'auto\');\n' +
					'  ga(\'send\', \'pageview\');\n' +
					'\n' +
					'</script>'
			options.bottom = '<i>Copyright &#169; 2015 Artem Dmitriev. All Rights Reserved.</i>'
			description = 'Aggregates Javadoc API documentation of all subprojects.'
			group = JavaBasePlugin.DOCUMENTATION_GROUP
			dependsOn javaSubprojects.javadoc

			source javaSubprojects.javadoc.source
			destinationDir rootProject.file("$rootProject.buildDir/docs/javadoc")
			classpath = rootProject.files(javaSubprojects.javadoc.classpath)
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "idea"
	apply plugin: "eclipse"
	apply plugin: "maven"

	group = 'org.reveno'
	version = "$revenoVersion"

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		compile "org.slf4j:slf4j-api:$slf4jVersion"
		compile "org.slf4j:slf4j-log4j12:$log4jVersion"

		testCompile "com.google.guava:guava:$guavaVersion"
		testCompile "junit:junit:$junitVersion"
		testCompile "org.easymock:easymock:$easymockVersion"
	}

	def forks = Math.max(1, (int) (Runtime.runtime.availableProcessors() / 2))

	test {
		minHeapSize = "512m"
		maxParallelForks = forks
		forkEvery = 1
		systemProperty 'protostuff.runtime.collection_schema_on_repeated_fields', 'true'

		testLogging {
			showExceptions = true
			showCauses = true
			showStackTraces = true
			events "passed", "skipped", "failed", "standardOut", "standardError"
		}
	}

	task wrapper(type: Wrapper) {
		gradleVersion = '2.6'
	}

	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
		options.setMemberLevel(org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC)
	}

	javadoc {
		title = '<h1>Reveno Framework </h1>'

		options.bottom = '<i>Copyright &#169; 2015 Artem Dmitriev. All Rights Reserved.</i>'
		options.addStringOption('XDignore.symbol.file', '-quiet')
		include '**/clustering/**'
		include '**/api/**'
		include '**/commons/**'
		include '**/core/**'
		include '**/utils/**'
		include '**/metrics/**'
		exclude '**/acceptance/**'
		exclude '**/test/**'
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives sourcesJar, javadocJar
	}

	defaultTasks 'clean', 'build'
}
